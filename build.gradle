plugins {
	id 'java'
	id 'com.google.protobuf' version '0.8.17'
	id 'application'
	id 'org.springframework.boot' version '2.6.1'
}

group = 'com.klausapp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
mainClassName = 'com.klausapp.scoringapi.ScoringApplication'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

def springBootVersion = '2.6.1'
def grpcVersion = '1.42.1'
def protobufVersion = '3.17.2'
def protocVersion = protobufVersion
def sqliteVersion = '3.36.0'
def lombokVersion = '1.18.16'

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
	implementation "org.springframework.boot:spring-boot-starter-jdbc:${springBootVersion}"
	implementation "org.xerial:sqlite-jdbc:${sqliteVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	implementation "net.devh:grpc-server-spring-boot-starter:2.12.0.RELEASE"
	implementation "org.projectlombok:lombok:${lombokVersion}"

	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}

startScripts.enabled = false

task scoringClient(type: CreateStartScripts) {
	mainClassName = 'com.klausapp.scoringapi.client.GrpcClient'
	applicationName = 'scoring-client'
	outputDir = new File(project.buildDir, 'tmp')
	classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
	from(scoringClient)
	fileMode = 0755
}
